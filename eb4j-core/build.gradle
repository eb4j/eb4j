
// Here is only place where plugins {} can be located.
plugins {
    // bintray
    id "com.jfrog.bintray" version "1.8.5"
    //
    id "com.github.spotbugs" version "4.5.1"
    //
    id 'idea'
    id 'java'
    id 'jacoco'
    id 'maven'
    id 'maven-publish'
    id 'java-library-distribution'
    id 'checkstyle'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    jcenter()
}

dependencies {
    testImplementation 'org.testng:testng:6.9.10'
}
test.useTestNG()

spotbugs {
    ignoreFailures = true
}

spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main.html")
        }
    }
}

checkstyle {
    config = resources.text.fromFile("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.16.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

version = projectVersion
group = projectGroup

def pomConfig = {
    licenses {
        license {
            name "The GNU Lesser General Public License, Version 2.1"
            url "https://www.gnu.org/licenses/old-licenses/lgpl-2.1.html"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "miurahr"
            name "Hiroshi Miura"
            email "miurahr@linux.com"
        }
    }

    scm {
        connection "scm:git:git://github.com/eb4j/eb4j.git"
        developerConnection "scm:git:git://github.com/eb4j/eb4j.git"
        url "https://github.com/eb4j/eb4j"
    }
}

publishing {
    publications {
        EB4JPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            groupId projectGroup
            artifactId projectName
            version projectVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Emerald API')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('bintrayUser')
    key = System.getenv('bintrayApiKey')
    configurations = ['archives']
    publications = ['EB4JPublication']
    dryRun=false
    publish=true
    override=true

    pkg {
        repo = 'maven'
        name = 'eb4j-core'
        licenses = ['LGPL-2.1']
        vcsUrl = projectUrl
        labels = ['java', 'EPWING', 'ebook']
        publicDownloadNumbers = true
        userOrg = 'eb4j'
        version {
            name = projectVersion
            description = 'EB4J'
            released  = new Date()
            vcsTag = 'v' + projectVersion
        }
    }
}

dependencies {
    compile 'commons-lang:commons-lang:2.6'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compileOnly 'org.slf4j:slf4j-nop:1.7.25'
    testImplementation 'org.slf4j:slf4j-nop:1.7.25'
    testImplementation 'org.testng:testng:6.9.10'
}
test.useTestNG()

distTar {
    compression = Compression.GZIP
}

distributions {
    main {
        distributionBaseName = 'eb4j-core'
        contents {
            from '../README.md'
            from '../LICENSE.txt'
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'eb4j-core'
                groupId 'io.github.eb4j'
                artifactId = 'eb4j-core'
                name 'eb4j'
                description = 'eb4j'
                url projectUrl
                licenses {
                    license {
                        name 'LGPL2.1+'
                    }
                }
            }
        }
    }
}
