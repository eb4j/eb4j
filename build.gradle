plugins {
    id 'idea'
    id 'java-library'
    id 'jacoco'
    id 'maven'
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
    id 'signing'
    id 'java-library-distribution'
    id 'checkstyle'
    id 'org.asciidoctor.jvm.convert' version '3.1.0'
    id 'com.palantir.git-version' version "0.12.3"
}

// calculate version string from git tag, hash and commit distance
if (versionDetails().isCleanTag) {
    // drop first 'v' from version tag
    version = gitVersion().substring(1)
} else {
    version = versionDetails().lastTag.substring(1) + '-' + versionDetails().commitDistance + '-' + versionDetails().gitHash + '-SNAPSHOT'
}

group = 'io.github.eb4j'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

// use asciidoc for javadoc
configurations {
    doclet
}

dependencies {
    doclet 'org.asciidoctor:asciidoclet:1.+'
    implementation 'commons-lang:commons-lang:2.6'
    api 'org.slf4j:slf4j-api:1.7.25'
    compileOnly 'org.slf4j:slf4j-nop:1.7.25'
    testImplementation 'org.slf4j:slf4j-nop:1.7.25'
    testImplementation 'org.testng:testng:6.9.10'
}
test.useTestNG()

checkstyle {
    config = resources.text.fromFile("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.16.1'
}

java {
    withSourcesJar()
    withJavadocJar()
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

javadoc {
    options.docletpath = configurations.doclet.files.asType(List)
    options.doclet = 'org.asciidoctor.Asciidoclet'
    options.setLocale("en")
    options.addStringOption "-base-dir", "${projectDir}"
    options.addStringOption "-attribute",
            "name=${project.name}," + "lang=ja," +
            "version=${project.version}," +
            "title-link=https://eb4j.github.io/eb4j[${project.name} ${project.version}]"
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            stagingProfileId = "a1d1cd5a9c0ab"
            username = project.hasProperty('sonatypeUsername') ? project.property('sonatypeUsername') : System.getenv('SONATYPE_USER')
            password = project.hasProperty('sonatypePassword') ? project.property('sonatypePassword') : System.getenv('SONATYPE_PASS')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "EB4J"
                description = "EPWING/Ebook access library"
                url = "https://github.com/eb4j/eb4j"
                licenses {
                    license {
                        name = "The GNU Lesser General Public License, Version 2.1"
                        url = "https://www.gnu.org/licenses/old-licenses/lgpl-2.1.html"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "miurahr"
                        name = "Hiroshi Miura"
                        email = "miurahr@linux.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/eb4j/eb4j.git"
                    developerConnection = "scm:git:git://github.com/eb4j/eb4j.git"
                    url = "https://github.com/eb4j/eb4j"
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/eb4j/eb4j"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            // FIXME: not working: error with 415 unsupported media type
            name = "GitLabPackages"
            url = "https://gitlab.com/api/v4/groups/dictzip/-/packages/maven"
            credentials(HttpHeaderCredentials) {
                def jobToken = System.getenv("CI_JOB_TOKEN")
                if (jobToken != null) {
                    // GitLab CI
                    name = "Job-Token"
                    value = System.getenv("CI_JOB_TOKEN")
                } else {
                    name = "Private-Token"
                    value = System.getenv("GITLAB_TOKEN")
                }
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
        maven {
            name 'AzurePackages'
            url 'https://pkgs.dev.azure.com/miurahr/github/_packaging/maven/maven/v1'
            credentials(PasswordCredentials) {
                username = System.getenv("AZURE_USER")
                password = System.getenv("AZURE_TOKEN")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    if (signingKey) {
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    } else {
        useGpgCmd()
    }
    sign publishing.publications.mavenJava
}
tasks.withType(Sign) {
    def hasKey = project.hasProperty("signingKey") || project.hasProperty("signing.gnupg.keyName")
    onlyIf { hasKey && versionDetails().isCleanTag }
}

distTar {
    compression = Compression.GZIP
}

distributions {
    main {
        distributionBaseName = 'eb4j'
        contents {
            from 'README.md'
            from 'LICENSE.txt'
        }
    }
}

// docs - github pages generation
asciidoctor {
    sourceDir  file('docs')
    sources {
        include 'index.adoc', 'about.adoc', 'links.adoc', 'reports.adoc'
    }
    outputDir  file('build/docs')
    attributes 'build-gradle': file('build.gradle'),
            'endpoint-url': 'http://eb4j.github.io/eb4j/',
            'source-highlighter': 'coderay',
            'imagesdir': 'images',
            'toc': 'left',
            'icons': 'font',
            'setanchors': 'true',
            'idprefix': '',
            'idseparator': '-',
            'docinfo1': 'true'
}
