// eb4j gradle build script.
//
// Copyright (c) 2016,2020-2021 Hiroshi Miura
//

plugins {
    id 'java'
    id 'jacoco'
    id 'maven'
    id 'distribution'
    id 'checkstyle'
    id 'application'
}

group = projectGroup
version = projectVersion

mainClassName = 'io.github.eb4j.tool.EBZip'
applicationName = 'ebzip'

repositories {
    mavenCentral()
    maven {
        url = uri("https://jitpack.io")
    }
    maven {
         name = "GitHubPackages"
         url = uri("https://maven.pkg.github.com/eb4j/eb4j")
    }
}

task ebdumpStartScript(type: CreateStartScripts) {
    mainClassName = 'io.github.eb4j.tool.EBDump'
    applicationName = 'ebdump'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
}

task ebinfoStartScript(type: CreateStartScripts) {
    mainClassName = 'io.github.eb4j.tool.EBInfo'
    applicationName = 'ebinfo'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
}

applicationDistribution.into("bin") {
    duplicatesStrategy= DuplicatesStrategy.EXCLUDE
    from(ebdumpStartScript)
    from(ebinfoStartScript)
    fileMode = 0755
}

dependencies {
    implementation project(':eb4j-core')
    implementation 'commons-cli:commons-cli:1.4'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'org.slf4j:slf4j-simple:1.7.25'
    testImplementation 'commons-io:commons-io:2.7'
    testImplementation 'org.testng:testng:6.9.10'
}
test.useTestNG()

distTar {
    compression = Compression.GZIP
}

distributions {
    main {
        distributionBaseName = 'eb4j-tools'
        contents {
            from '../README.md'
            from '../LICENSE.txt'
        }
    }
}

checkstyle {
    config = resources.text.fromFile("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.16.1'
}
